/*
Copyright (c) 2011, Los Alamos National Security, LLC
All rights reserved.
Copyright 2011. Los Alamos National Security, LLC. This software was produced under U.S. Government contract DE-AC52-06NA25396 for Los Alamos National Laboratory (LANL),
which is operated by Los Alamos National Security, LLC for the U.S. Department of Energy. The U.S. Government has rights to use, reproduce, and distribute this software.

NEITHER THE GOVERNMENT NOR LOS ALAMOS NATIONAL SECURITY, LLC MAKES ANY WARRANTY, EXPRESS OR IMPLIED, OR ASSUMES ANY LIABILITY FOR THE USE OF THIS SOFTWARE.

If software is modified to produce derivative works, such modified software should be clearly marked, so as not to confuse it with the version available from LANL.

Additionally, redistribution and use in source and binary forms, with or without modification, are permitted provided that the following conditions are met:
·         Redistributions of source code must retain the above copyright notice, this list of conditions and the following disclaimer.
·         Redistributions in binary form must reproduce the above copyright notice, this list of conditions and the following disclaimer in the documentation and/or other
          materials provided with the distribution.
·         Neither the name of Los Alamos National Security, LLC, Los Alamos National Laboratory, LANL, the U.S. Government, nor the names of its contributors may be used
          to endorse or promote products derived from this software without specific prior written permission.

THIS SOFTWARE IS PROVIDED BY LOS ALAMOS NATIONAL SECURITY, LLC AND CONTRIBUTORS "AS IS" AND ANY EXPRESS OR IMPLIED WARRANTIES, INCLUDING, BUT NOT LIMITED TO, THE IMPLIED
WARRANTIES OF MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE ARE DISCLAIMED. IN NO EVENT SHALL LOS ALAMOS NATIONAL SECURITY, LLC OR CONTRIBUTORS BE LIABLE FOR ANY
DIRECT, INDIRECT, INCIDENTAL, SPECIAL, EXEMPLARY, OR CONSEQUENTIAL DAMAGES (INCLUDING, BUT NOT LIMITED TO, PROCUREMENT OF SUBSTITUTE GOODS OR SERVICES; LOSS OF USE, DATA,
OR PROFITS; OR BUSINESS INTERRUPTION) HOWEVER CAUSED AND ON ANY THEORY OF LIABILITY, WHETHER IN CONTRACT, STRICT LIABILITY, OR TORT (INCLUDING NEGLIGENCE OR OTHERWISE)
ARISING IN ANY WAY OUT OF THE USE OF THIS SOFTWARE, EVEN IF ADVISED OF THE POSSIBILITY OF SUCH DAMAGE.
*/
/*
 * mandelbrot_field.h
 *
 *  Created on: Feb 6, 2012
 *      Author: ollie
 */

#ifndef MANDELBROT_FIELD_H_
#define MANDELBROT_FIELD_H_

#include <piston/image2d.h>
#include <piston/choose_container.h>
#include <piston/implicit_function.h>

namespace piston {

template <typename MemorySpace>
struct mandelbrot_field : public piston::image2d<MemorySpace>
{
    typedef piston::image2d<MemorySpace> Parent;

    //TODO: move this to parent class?
    typedef typename thrust::iterator_traits<typename Parent::GridCoordinatesIterator>::value_type
	    GridCoordinatesType;

    // transform grid_coordinates (i, j) generated by image2d to shifted and
    // scaled physical_coordinates (x, y)
    struct physical_coordinates_functor : public thrust::unary_function<GridCoordinatesType,
								        thrust::tuple<float, float> >
    {
	const float xmin;
	const float ymin;
	const float deltax;
	const float deltay;

	physical_coordinates_functor(float xmin, float ymin, float deltax, float deltay) :
	    xmin(xmin), ymin(ymin), deltax(deltax), deltay(deltay) {}

	__host__ __device__
	thrust::tuple<float, float> operator() (const GridCoordinatesType& grid_coord) const {
	    const float x = xmin + deltax * thrust::get<0>(grid_coord);
	    const float y = ymin + deltay * thrust::get<1>(grid_coord);

	    return thrust::make_tuple(x, y);
	}
    };

    // It is VERY IMPORTANT to declare grid_coordinates_iterator before
    // point_data_iterator since C++ initialize data members in the order
    // of their declaration and point_data_iterator depends on grid_coordinates
    // to be initialized first.
    typedef thrust::transform_iterator<physical_coordinates_functor,
	    typename Parent::GridCoordinatesIterator> PhysicalCoordinatesIterator;
    PhysicalCoordinatesIterator phys_coordinates_iterator;
    typedef typename thrust::iterator_traits<PhysicalCoordinatesIterator>::value_type
	    PhysicalCoordinatesType;

    // transform (x, y) physical coordinates into number of iterations
    struct mandelbrot_functor : public thrust::unary_function<PhysicalCoordinatesType, unsigned>
    {
	static const int MAX_ITERS = 16;
	static const float MAX_R2 = 100.0f;

	__host__ __device__
	unsigned operator() (const PhysicalCoordinatesType& pos) const {
	    unsigned iters = 0;
	    // z = (0, 0) initially
	    float x = 0;
	    float y = 0;
	    // p
	    float u = thrust::get<0>(pos);
	    float v = thrust::get<1>(pos);
	    // |z|^2
	    float xx = x*x;
	    float yy = y*y;
	    float r2 = xx + yy;

	    // iterate z = z^2 + p, |z|^2 < MAX_R2
	    while (r2 < MAX_R2 && iters++ < MAX_ITERS) {
		y = 2*x*y   + v;
		x = xx - yy + u;
		xx = x*x;
		yy = y*y;
		r2 = xx+yy;
	    }

	    return iters;
	}
    };

    typedef thrust::transform_iterator<mandelbrot_functor,
				       PhysicalCoordinatesIterator> PointDataIterator;
    PointDataIterator point_data_iterator;

    mandelbrot_field(unsigned dim0, unsigned dim1,
                     float xmin, float ymin,
                     float xmax, float ymax) :
	Parent(dim0, dim1),
	phys_coordinates_iterator(Parent::grid_coordinates_iterator,
	                          physical_coordinates_functor(xmin, ymin, (xmax-xmin)/dim0, (ymax-ymin)/dim1)),
	point_data_iterator(phys_coordinates_iterator, mandelbrot_functor())
    {}

    PhysicalCoordinatesIterator physical_coordinates_begin() {
	return phys_coordinates_iterator;
    }
    PhysicalCoordinatesIterator physical_coordinates_end() {
	return phys_coordinates_iterator+this->NPoints;
    }

    PointDataIterator point_data_begin() {
	return point_data_iterator;
    }
    PointDataIterator point_data_end() {
	return point_data_iterator+this->NPoints;
    }
};

}


#endif /* MANDELBROT_FIELD_H_ */
